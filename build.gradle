plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

group = 'com.ring-ding-dong'
version = '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    // Java 8 호환성을 위한 컴파일 옵션
    options.release = 8
}

repositories {
    mavenCentral()
}

dependencies {

    // 테스팅
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Jobserv'
                description = 'Java Observability Toolkit'
                url = 'https://github.com/ring-ding-dong/jobserv'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'chimaek'
                        name = 'Seung ju Kim'
                        email = 'pipiru100@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ring-ding-dong/jobserv.git'
                    developerConnection = 'scm:git::ssh://github.com/ring-ding-dong/jobserv.git'
                    url = 'https://github.com/ring-ding-dong/jobserv'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}